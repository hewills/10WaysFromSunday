// room script file
int musicplaying = 0;

// Set room background
function room_Load()
{

  if (comm_done < 3) {
     SetBackgroundFrame(0);
     hBox.Enabled = false;
     hMop.Enabled = false;
     oDraino.Visible = false;
  }
  else {
  // Open Utility closet
  
  SetBackgroundFrame(1);
  
    //If Debra hasn't picked up Draino yet
    if (!cDebra.HasInventory(iDraino) && comm_done <= 5) {
        oDraino.Visible = true;
     }
     hBox.Enabled = true;
     hMop.Enabled = true;
  }
  
}

function room_AfterFadeIn()
{
  if (comm_done >= 3 && comm_done <= 5) {
   if (!organist_dead && !receptionist_dead) {
     
    if (dminor != null) {
          
         if (dminor.IsPlaying) {
            dminor.Volume = 5;
         }
         else {
          dminor = a10Dminor.Play(); 
          dminor.Volume = 5;
         }
    }
    else {
      dminor = a10Dminor.Play();
      dminor.Volume = 5;
       } 
   }   
  }  
}


function room_FirstLoad()
{
  //Congregation singing
  AudioChannel *music = aGodbewithyou.Play();
  music.Volume = 30;
  
  cDebra.Say("I keep dreaming of Timothy.");
  cDebra.Say("Ever since his passing, he won't leave my dreams.");
  cDebra.Say("Something is wrong.");
  cDebra.Say("His soul is lost. And I pray, but get no answers.");
  cDebra.Say("I need to get God's attention.");
  cDebra.Say("But how?");
  cDebra.Say("Maybe this week's Bible study guide will inspire me.");

 musicplaying = 1;
 
 Display("TO PLAY:  Left-click to Interact. Right-click to Look");
 
}


function room_Leave()
{
  if (dminor != null && dminor.IsPlaying)
  dminor.Stop();
}

function hBoard_Look()
{ 
  cDebra.Say("A few postings from church members.");
}

function hBoard_Interact()
{
  cDebra.Say("I don't need any postings.");
}


function hSanctuary_Look()
{
  cDebra.Say("The door into the sanctuary.");
}

function hSanctuary_Interact()
{
  if(musicplaying) {
    cDebra.Say("I don't want to return to the service. I'll just have another dream about Timothy.");
  }
  else {
  cDebra.ChangeRoom(13, 154, 154, eDirectionUp); 
  }
}

function hWindow_Look()
{
  if(musicplaying) {
    cDebra.Say("I can see the congregation singing.");
  }
  else
  cDebra.Say("I can see the sanctuary in there.");
}

function hWindow_Interact()
{
  cDebra.Say("The window doesn't open.");
}

function hCloset_Look()
{
  cDebra.Say("It's the utility closet.");
}

function hCloset_Interact()
{
  if (GetBackgroundFrame() == 0) {
  cDebra.Say("The closet is locked.");
  }
  else cDebra.Say("The custodian must have been here.");
}

function hExit_Look()
{
  cDebra.Say("It's the exit.");
}

function hTract_Look()
{
  cDebra.Say("It's the pile of study guides.");
}

function hTract_Interact()
{
  
  if (cDebra.HasInventory(iList)) {
    
    cDebra.Say("I already have one."); 
    
  }
  else {
      cDebra.Say("I'll grab a study guide.");
      cDebra.AddInventory(iList);
      
      showList();
      UnPauseGame();
      cDebra.Say("This weeks guide is on the ten commandments.");
      cDebra.Say("I've tried my best to follow these every since I was a little.");
      cDebra.Say("That's it!");
      cDebra.Say("That's how I'll get God's attention.");
      cDebra.Say("If He sees me breaking each one of His commandments, one by one, in order...");
      cDebra.Say("He'll for sure look my way.");
      cDebra.Say("And probably pretty quickly.");
      cDebra.Say("I need to help Timothy.");
      cDebra.Say("And God could save him.");  
}

}

function hExit_Interact()
{

  if (!cDebra.HasInventory(iList)) {
    
    cDebra.Say("I want a study guide before I go.");
  }
  else {    
    //Stop congregation singing. No more of that.
    aGodbewithyou.Stop();
    musicplaying = 0;    
    showGPS();   
  }  
}

function hMop_Look()
{
  cDebra.Say("It's the custodian's mop.");
}

// Sheet music Box
function hBox_Look()
{
  cDebra.Say("It's a box of sheet music and old church bulletins.");
}

function hBox_Interact()
{
  if (comm_done == 5 && look_at_decoration == 1 && !cDebra.HasInventory(iMusic) && !organist_dead) {
    
   cDebra.Say("I'll take this sheet music with a lot of bass.");
   cDebra.AddInventory(iMusic);
  }
  else if (look_at_decoration == 0) {
   cDebra.Say("I have no need for sheetmusic or old bulletins.");
  }
  else {
   cDebra.Say("There's nothing else I need in here."); 
  }

}

// Draino
function oDraino_Look()
{
  cDebra.Say("A bottle of industrial-strength Draino.");
}

function oDraino_Interact()
{ cDebra.Walk(315, 167, eBlock, eWalkableAreas);
  cDebra.Say("I'm sure the custodian won't mind me borrowing it.");
  cDebra.Say("I hate clogged drains.");
  cDebra.AddInventory(iDraino);
  oDraino.Visible = false;
    
}

