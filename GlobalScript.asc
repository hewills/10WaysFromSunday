// Global Variables

/*
0 None
1	No other gods before me
2	No graven images
3	Do not take the Lord's name in vain
4	Keep the Sabbath Holy
5	Honor thy mother and father
6	Do not kill
7	Do not commit adultery
8	Do not steal
9	Do not perjure
10	Do not covet
*/

// A function that initializes a bunch of stuff. -----------------------------------------------------
function initialize_control_panel() { 

  // Center the control panel
  gControlPanel.Centre();
    
  // Set all the defaults
  System.Volume = 70;
  sldAudio.Value = System.Volume;
  SetGameSpeed(60);
  sldSpeed.Value =60;
  
 Game.NormalFont = eFontMinecraftia;  //eFontMinecraftia  eFont8bit  eFontfntTiny Dialog font
 


}
// ------------------- End Initialize ----------------------------------------------------------------


//----------------------------------------------------------------------------------------------------
// game_start
//----------------------------------------------------------------------------------------------------
function game_start() 
{
  lGPS.AddItem("Home");
  lGPS.AddItem("Church");
  lGPS.AddItem("Work");
  lGPS.AddItem("Downtown");
  gGPS.SetPosition(80, 50);
  
  initialize_control_panel();
  
  cTeen.ChangeRoom(11); //hangs in heaven until we're ready for him/her
  cCashier.ChangeRoom(11); //hangs in heaven until we're ready for him/her
  
}

//----------------------------------------------------------------------------------------------------
// repeatedly_execute
//----------------------------------------------------------------------------------------------------
function repeatedly_execute() 
{
}

//----------------------------------------------------------------------------------------------------
// repeatedly_execute_always
//----------------------------------------------------------------------------------------------------
function repeatedly_execute_always() 
{
}


// ------------------- Save Game Dialog --------------------------------------------------------------
function show_save_game_dialog()
{
  
  gSaveGame.Visible = true;
  // Get the list of save games
  lstSaveGamesList.FillSaveGameList();
  
  if (lstSaveGamesList.ItemCount > 0)
  {
    // If there is at least one, set the default text
    // to be the first game's name
    txtNewSaveName.Text = lstSaveGamesList.Items[0];
  }
  else
  {
    // No save games yet, default empty text.
    txtNewSaveName.Text = "";
  }
  mouse.UseModeGraphic(eModePointer);

}
// ------------------End Save Dialog------------------------------------------------------------------


//------------------ Restore Game Dialog -------------------------------------------------------------
function show_restore_game_dialog()
{
  gRestoreGame.Visible = true;
  lstRestoreGamesList.FillSaveGameList();
  mouse.UseModeGraphic(eModePointer);
  
}
//------------------End Restore Dialog---------------------------------------------------------------

// --- SAVE functions ----
function close_save_game_dialog()
{
  gSaveGame.Visible = false;
  mouse.UseDefaultGraphic();
}

function btnCancelSave_OnClick(GUIControl *control, MouseButton button)
{
  close_save_game_dialog();
}

function btnSaveGame_OnClick(GUIControl *control, MouseButton button)
{
  int gameSlotToSaveInto = lstSaveGamesList.ItemCount + 1;
  int i = 0;
  
  while (i < lstSaveGamesList.ItemCount)
  {
    if (lstSaveGamesList.Items[i] == txtNewSaveName.Text)
    {
      gameSlotToSaveInto = lstSaveGamesList.SaveGameSlots[i];
    }
    i++;
  }
  SaveGameSlot(gameSlotToSaveInto, txtNewSaveName.Text);
  close_save_game_dialog();
}


function lstSaveGamesList_OnSelectionCh(GUIControl *control)
{
  txtNewSaveName.Text = lstSaveGamesList.Items[lstSaveGamesList.SelectedIndex];
}


function txtNewSaveName_OnActivate(GUIControl *control)
{
  // Pressing return in the text box simulates clicking the Save button
  btnSaveGame_OnClick(control, eMouseLeft);
}

function btnDeleteSave_OnClick(GUIControl *control, MouseButton button)
{
  if (lstSaveGamesList.SelectedIndex >= 0)
  {
    DeleteSaveSlot(lstSaveGamesList.SaveGameSlots[lstSaveGamesList.SelectedIndex]);
    lstSaveGamesList.FillSaveGameList();
  }
}
// ------------ End SAVE functions -------------


// ----- RESTORE functions ---------------------

function close_restore_game_dialog()
{
  gRestoreGame.Visible = false;
  mouse.UseDefaultGraphic();
}

function btnCancelRestore_OnClick(GUIControl *control, MouseButton button)
{
  close_restore_game_dialog();
}


function btnRestoreGame_OnClick(GUIControl *control, MouseButton button)
{
  if (lstRestoreGamesList.SelectedIndex >= 0)
  {
    RestoreGameSlot(lstRestoreGamesList.SaveGameSlots[lstRestoreGamesList.SelectedIndex]);
  }
  close_restore_game_dialog();
}
// ---- End RESTORE functions


//Loading
function btnLoad_OnClick(GUIControl *control, MouseButton button)
{
  gControlPanel.Visible = false;
  mouse.UseDefaultGraphic();
  Wait(1);
  show_restore_game_dialog();
}

//Resume
function btnResume_OnClick(GUIControl *control, MouseButton button)
{
  gControlPanel.Visible = false;
  mouse.UseDefaultGraphic();
}


//Inventory Quit
function btnQuit_OnClick(GUIControl *control, MouseButton button)
{
  //gControlPanel.Visible = false;
  //Wait(1);
  //QuitGame(1);
  gControlPanel.Visible = true;
  mouse.UseModeGraphic(eModePointer);
}

//Control Panel Quit
function btnQuit2_OnClick(GUIControl *control, MouseButton button)
{
  gControlPanel.Visible = false;
  Wait(1);
  QuitGame(1);
  gControlPanel.Visible = true;
  mouse.UseModeGraphic(eModePointer);
}

//Audio Change
function sldAudio_OnChange(GUIControl *control)
{
  System.Volume = sldAudio.Value;
}

//Speed Change
function sldSpeed_OnChange(GUIControl *control)
{
  SetGameSpeed(sldSpeed.Value);
}

function btnSave_OnClick(GUIControl *control, MouseButton button)
{
    gControlPanel.Visible = false;
    show_save_game_dialog();
}



//----------------------------------------------------------------------------------------------------
// on_key_press
//----------------------------------------------------------------------------------------------------
function on_key_press(eKeyCode keycode) 
{
 
  if (IsGamePaused()) keycode = 0;
  
  // "System Keys"
  if (keycode == eKeyCtrlQ) QuitGame(1); // Ctrl-Q
  //if (keycode == eKeyF9) RestartGame(); // F9
  if (keycode == eKeyF12) SaveScreenShot("scrnshot.pcx");  // F12
  
  // Debugger Keys
  //if (keycode == eKeyCtrlS) Debug(0,0); // Ctrl-S, give all inventory
  if (keycode == eKeyCtrlV) Debug(1,0); // Ctrl-V, version
  //if (keycode == eKeyCtrlA) Debug(2,0); // Ctrl-A, show walkable areas
  //if (keycode == eKeyCtrlX) Debug(3,0); // Ctrl-X, teleport to room
  
  
  if (keycode == eKeyCtrlG) {
    
      if (gGPS.Visible) { 
          gGPS.Visible = false;
      }
      else 
          gGPS.Visible = true; // show GPS
      
  }
  
  if (keycode == eKeyCtrlL) {
    
      if (gList.Visible) { 
          gList.Visible = false;
      }
      else 
          gList.Visible = true; // show 10 commandments
      
  }
  
 if ((keycode == eKeyEscape) && (gSaveGame.Visible))
  {
    // Use ESC to close the save game dialog
    close_save_game_dialog();
    return;
  }
  
  
  if ((keycode == eKeyEscape) && (gRestoreGame.Visible))
  {
    // Use ESC to close the restore game dialog
    close_restore_game_dialog();
    return;
  }
  
  if ((keycode == eKeyEscape) && gControlPanel.Visible) {
    // Use ESC to turn the panel off.
    gControlPanel.Visible = false; 
    mouse.UseDefaultGraphic();
    return;
  }
  
    // FUNCTION KEYS AND SYSTEM SHORTCUTS
  if (keycode == eKeyEscape) {
    // ESC
    gControlPanel.Visible = true; 
    mouse.UseModeGraphic(eModePointer);
  }
}

//----------------------------------------------------------------------------------------------------
// on_mouse_click
//----------------------------------------------------------------------------------------------------
function on_mouse_click(MouseButton button)
{
	// all mouse clicks are handled in TwoClickHandler.asc!
}

//----------------------------------------------------------------------------------------------------
// on_event
//----------------------------------------------------------------------------------------------------
function on_event (EventType event, int data) 
{
}

//----------------------------------------------------------------------------------------------------
// unhandled_event
//----------------------------------------------------------------------------------------------------
function unhandled_event (int what, int type) 
{
	if (what == 1) // Unhandled events for HOTSPOTS
	{
		if (type == 1) // look
		{
			cDebra.Say("Meh. Nothing special.");
		}
		if (type == 2) // interact
		{
			cDebra.Say("I can't do anything with it.");
		}
		if (type == 3) // use inv on
		{
			cDebra.Say("I have no reason to do that.");
		}
	}
	if (what == 2) // Unhandled events for OBJECTS
	{
		if (type == 0) // look
		{
			cDebra.Say("Looks like a thing.");
		}
		if (type == 1) // interact
		{
			cDebra.Say("I don't want it.");
		}
		if (type == 3) // use inv on
		{
			cDebra.Say("I have no reason to do that.");
		}
	}
	if (what == 3) // Unhandled events for CHARACTERS
	{
		if (type == 0) // look
		{
			cDebra.Say("Another one of God's children.");
		}
		if (type == 1) // interact
		{
			cDebra.Say("I don't have anything to say.");
		}
		if (type == 3) // use inv on
		{
			cDebra.Say("I don't think I should give that away.");
		}
	}
	if (what == 5) // Unhandled events for INVENTORY ITEMS
	{
		if (type == 0) // look
		{
			cDebra.Say("Just stuff in my pocket.");
		}
		if (type == 1) // interact
		{
			cDebra.Say("Um, no?");
		}
		if (type == 3) // use inv on
		{
			cDebra.Say("That's ridiculous.");
		}
	}
}

//----------------------------------------------------------------------------------------------------
// dialog_request
//----------------------------------------------------------------------------------------------------
function dialog_request(int param) 
{
}

//----------------------------------------------------------------------------------------------------
// gInventoryBar
//----------------------------------------------------------------------------------------------------
function btnInvScrollLeft_OnClick(GUIControl *control, MouseButton button)
{
	InventoryWindow1.ScrollDown();
}

function btnInvScrollRight_OnClick(GUIControl *control, MouseButton button)
{
	InventoryWindow1.ScrollUp();
}


//Church Sign buttons
function bQuit_OnClick(GUIControl *control, MouseButton button)
{
  QuitGame(0);
}

function bOptions_OnClick(GUIControl *control, MouseButton button)
{
  gControlPanel.Visible = true;
  mouse.UseModeGraphic(eModePointer);
}

function bStart_OnClick(GUIControl *control, MouseButton button)
{
  gSign.Visible = false;
  aGodbewithyou.Stop();
  
  //Start Game
  cDebra.ChangeRoom(1, 380, 213);
  
}

// Satan Arrives Event
function satanArrives() {
  
  satan_arrives = 1;
  cDebra.ChangeRoom(3, 162, 202, eDirectionDown);
  
}

// Show 10 commandments
function showList()  {
  
  if (comm_done == 0) {    
   gList.BackgroundGraphic = 109;
  }
  else if (comm_done == 1) {
   gList.BackgroundGraphic = 110; 
  }
  else if (comm_done == 2) {
   gList.BackgroundGraphic = 111;     
  }
  else if (comm_done == 3) {
   gList.BackgroundGraphic = 112;
  }
  else if (comm_done == 4) {
   gList.BackgroundGraphic = 113; 
  }
  else if (comm_done == 5) {
   gList.BackgroundGraphic = 114;
  }
  else if (comm_done == 6) {
   gList.BackgroundGraphic = 115;     
  }
  else if (comm_done == 7) {
   gList.BackgroundGraphic = 116;
  }
  else if (comm_done == 8) {
   gList.BackgroundGraphic = 117;
  }
  else if (comm_done == 9) {
   gList.BackgroundGraphic = 118;    
  }
  else if (comm_done == 10) {
   gList.BackgroundGraphic = 119;
  }
  else gList.BackgroundGraphic = 109;

  PauseGame();
  gList.Visible = true;
  
}
// 10 commandments close Button
function bListClose_OnClick(GUIControl *control, MouseButton button)
{
  gList.Visible = false;
  UnPauseGame();
  
  if (comm_done == 5 && satan_arrives == 0) {
    satanArrives();
  }
  
  if (comm_done == 10) {
      cDebra.ChangeRoom(15, 180, 186);
  }
}
// Update commandment list
function updateList(int number_done) {
 
 aThunder.Play();
 comm_done = number_done;
  
}

// Click on 10 Commandments GUI list
function gList_OnClick(GUI *theGui, MouseButton button)
{
  //Give player a hint when clicking on the commandments

if (comm_done == 0) {    
   cDebra.SayAt(40, 225, 200, "#1 says I need to find another god to worship.");
  }
  else if (comm_done == 1) {
   cDebra.SayAt(40,225,200,"For #2, building a representation of God shouldn't be too hard."); 
  }
  else if (comm_done == 2) {
   cDebra.SayAt(40,225,200,"To make #3 count, I need to say the Lord's name on a greater scale.");    
  }
  else if (comm_done == 3) {
   cDebra.SayAt(40,225,200,"#4 says do something unholy on Sunday? Shouldn't be a problem.");
  }
  else if (comm_done == 4) {
   cDebra.SayAt(40,225,200,"#5. Hmm. My parents ARE on speed dial."); 
  }
  else if (comm_done == 5) {
   cDebra.SayAt(40,225,200,"I was hoping God would show up before #6. I don't know if I can do this.");
  }
  else if (comm_done == 6) {
   cDebra.SayAt(40,225,200,"Yikes! I guess step one for #7 is finding someone married.");    
  }
  else if (comm_done == 7) {
   cDebra.SayAt(40,225,200,"#8 is basically stealing something that isn't mine.");
  }
  else if (comm_done == 8) {
   cDebra.SayAt(40,225,200,"#9 means I need to tell a lie, and plant some evidence.");
  }
  else if (comm_done == 9) {
   cDebra.SayAt(40,225,200,"#10 is all about coveting my neighbor. Easy peasy.");   
  }
  else cDebra.SayAt(40,225,200,"Hmm");

}


// GPS GUI List
function showGPS() {
  
  gGPS.SetPosition(80, 50);
  gGPS.Visible = true;
  PauseGame();
   
}
// GPS Cancel Button
function bCancel_OnClick(GUIControl *control, MouseButton button)
{
  UnPauseGame();
  gGPS.Visible = false;
}
// GPS Go Button
function bGPS_go_OnClick(GUIControl *control, MouseButton button)
{
  UnPauseGame();
  gGPS.Visible = false;
  
  String selectedLoc = "Home";
  
  if (lGPS.SelectedIndex >= 0)
  {
      selectedLoc = lGPS.Items[lGPS.SelectedIndex];
  

    if (selectedLoc == "Home") {
   
      //mHoly.Play();
      cDebra.ChangeRoom(3, 90, 180);
    
    }
    else if (selectedLoc == "Work") {
    
      //mCrown.Play();
      cDebra.ChangeRoom(4, 120, 205, eDirectionUp);
   
    }
    else if (selectedLoc == "Church") {
    
      cDebra.ChangeRoom(1, 433, 218);
   
    }
    else if (selectedLoc == "Downtown") {
    
      //mCross.Play();
      cDebra.ChangeRoom(8, 130, 215, eDirectionUp);
   
    }
    else if (selectedLoc == "Mercury Motors") {
    
      //mHoly.Play();
      cDebra.ChangeRoom(7, 65, 215, eDirectionUp);
   
    }
      else cDebra.ChangeRoom(3, 82, 175); //default Home
  
  }
}

///// INVENTORY items  ////////////////

function iList_Look()
{
    showList();   
}

function iDraino_Look()
{
  cDebra.Say("A bottle of Draino. 'Do not get in eyes. Do not consume.'");
}

function iMarker_Look()
{
  cDebra.Say("My black magic marker.");
}

function iMusic_Look()
{
  cDebra.Say("It's organ sheet music. It's called 'Toccata', and has a lot of bass notes.");
}

function iScissors_Look()
{
  cDebra.Say("A pair of scissors.");
}

function iRing_Look()
{
  cDebra.Say("A man's gold wedding ring.");
}

// Cell Phone
function iPhone_Look()
{
 PauseGame();
 gPhone.Visible = true; 
}
function gPhone_OnClick(GUI *theGui, MouseButton button)
{
  UnPauseGame();
  gPhone.Visible = false;
     
  if (comm_done == 4) {
  
     phoneRinging = aRinging.Play(eAudioPriorityHigh, eOnce);
     while (phoneRinging.IsPlaying)  Wait(1);
     
          phoneRinging = aRinging.Play(eAudioPriorityHigh, eOnce);
     while (phoneRinging.IsPlaying)  Wait(1);
     
     Wait(75);

     cMom.SayAt(70, 191,200, "Hi dear, it's so nice to have you calling.");
     cDad.SayAt(70, 191,200, "Oh is that Debra? Hi Honey.");
     cDebra.Say("Hi Mom. Hi Dad.");
     dParents.Start();
  }
  else {
   cDebra.Say("I don't need to call anyone.");  
  }
}
function bClosephone_OnClick(GUIControl *control, MouseButton button)
{
  UnPauseGame();
  gPhone.Visible = false;
}


function iWood_Look()
{
  cDebra.Say("Pieces of wood.");
}

function iPaper_Look()
{
  cDebra.Say("A blank piece of paper.");
}

function iNails_Look()
{
  cDebra.Say("A handful of metal nails.");
}

function iMoney_Look()
{
  cDebra.Say("Ten bucks!");
}


// Susan the Receptionist interactions
function cSusan_Look()
{
  cDebra.Say("That's Susan the receptionist.");
  cSusan.Say("Who are you talking to?");
}


function cSusan_Interact()
{
  cDebra.Walk(176, 186, eBlock, eWalkableAreas);
  
  if (comm_done == 0) {  
      dFirst.Start();
  }
  else if (comm_done == 1) {  
      dSecond.Start();
  }
  else cDebra.Say("She looks busy. I don't want to bother her.");   
  
}
///  END OF SUSAN the Receptionist


/// Combine Inventory section
function iWood_UseInv()
{
  if ( comm_done > 0 && cDebra.ActiveInventory == iNails) {
    
   cDebra.Say("I'll make a frame!");
   cDebra.Say("Now I just need something I can draw on.");
   cDebra.LoseInventory(iNails);
   cDebra.LoseInventory(iWood);
   cDebra.AddInventory(iFrame);   
  }
  else cDebra.Say("I have no reason to mess with this now.");
    
}

function iNails_UseInv()
{
  if ( comm_done > 0 && cDebra.ActiveInventory == iWood) {
    
   cDebra.Say("I'll make a frame!");
   cDebra.Say("Now I just need something I can draw on.");
   cDebra.LoseInventory(iNails);
   cDebra.LoseInventory(iWood);
   cDebra.AddInventory(iFrame);
  }
  else cDebra.Say("I have no reason to mess with this now.");
}

function iFrame_UseInv()
{
  if ( comm_done > 0 && cDebra.ActiveInventory == iPaper) {
    
    cDebra.Say("This paper fits perfectly in my frame.");
    cDebra.Say("My graven image is almost done.");
    cDebra.LoseInventory(iPaper);
    iFrame.Graphic = 150;     //Paper is inside frame
  
  }
  else if ( comm_done > 0 && cDebra.ActiveInventory == iMarker && iFrame.Graphic == 150) {
    
    cDebra.Say("Hmm. This requires a better surface.");
    cDebra.Say("I should use my craft table.");
  
  }
  else cDebra.Say("That's silly.");
    
}

function iPaper_UseInv()
{
    if ( comm_done > 0 && cDebra.ActiveInventory == iFrame) {
    
    cDebra.Say("This paper fits perfectly in my frame.");
    cDebra.Say("My graven image is almost done.");
    cDebra.LoseInventory(iPaper);
    iFrame.Graphic = 150;
  }
  else cDebra.Say("That's silly.");
}

function iFrame_Look()
{
  cDebra.Say("My self-made frame of wood.");
}


/// End combining Inventory


// Teen Interaction
function cTeen_Look()
{
  cDebra.Say("That teen smoking, looks like trouble.");
  cTeen.Say("Your FACE is trouble.");
}

function cTeen_Interact()
{
  if (talked_to_teen == 0) {
    dTeen1.Start();
  }
  else if (talked_to_teen > 1) {
    dTeen2.Start();
  }

}

function cTeen_UseInv()
{
  if (cDebra.ActiveInventory == iMoney && talked_to_teen == 1) {
    
   cTeen.Say("Thanks");
   cTeen.Say("I'll make sure the alarm goes off, so you can do your 'thing'.");
   cTeen.Say("Weirdo.");
   cDebra.LoseInventory(iMoney);
   talked_to_teen = 2;
   
  } else cDebra.Say("Why would I give away my stuff for no reason?");
    
}

// Movie Cashier interactions
function cCashier_Look()
{
  cDebra.Say("I wonder if she gets to see movies for free.");
  cCashier.Say ("I do!");
}

function cCashier_Interact()
{
  if (read_movie_sign == 0) {
    dCashier_presign.Start();
  }
  else if (read_movie_sign == 1) {
    dCashier_sign.Start();    
  }
  else {
    cDebra.Say("I don't have time for anymore movies.");  
  }
  
}


function cBar_Look()
{
    cDebra.Walk(147, 140, eBlock, eWalkableAreas);
  cDebra.Say("The bartender doesn't seem happy to see me.");
}

function cBar_Interact()
{
    cDebra.Walk(147, 140, eBlock, eWalkableAreas);
  dBartender.Start();
}

function cSkip_Look()
{
  cDebra.Walk(149, 187, eBlock, eWalkableAreas);
  
  cDebra.Say("Nice suit!");
  
  if (comm_done == 6) {
   saw_ring = 1;
   cDebra.Say("He seems nice. And he has a ring on his finger.");    
  }
  
}

function leave_bar() {
  
 cSkip.ChangeView(28);
 cSkip.Animate(0, 10, eOnce, eBlock, eForwards);
 cSkip.ChangeView(26);
 cSkip.Animate(0, 30, eOnce, eBlock);
 Wait(40);
 FadeOut(70);
 cSkip.ChangeRoom(11);
 cDebra.ChangeRoom(3, 200, 200, eDirectionDown);
  
}

function cSkip_Interact()
{
  cDebra.Walk(149, 187, eBlock, eWalkableAreas);
  
  if (comm_done < 6) { 
   cDebra.Say("Hello!");
   cSkip.Say("Hi!");
  }
  
  if (comm_done == 6 && !saw_ring) {
    
    cDebra.Say("I better make sure he's married first.");
  }
  
  if (comm_done == 6 && saw_ring) {
    
    dSkip_meetcute.Start();    
    
  } 

}

function cBob_Look()
{
  cDebra.Say("That's my neighbor Bob.");
}

function cBob_Interact()
{
  dBob1.Start();

}

