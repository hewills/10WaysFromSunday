// room script file
int draino_in_cup = 0;

function susanReturns()  {
  
   //Comes back inside
   cSusan.ChangeRoom(5, 10, 230, eDirectionDown);
   cSusan.Say("&10 False alarm.");
   talked_to_teen = 3;
   
   //Walks back to desk
   cSusan.Walk(10, 220, eBlock, eWalkableAreas);
   cSusan.Walk(264, 216, eBlock, eWalkableAreas);
   cSusan.Walk(295, 180, eBlock, eWalkableAreas);
   cSusan.Walk(256, 177, eBlock, eWalkableAreas);
   cSusan.FaceDirection(eDirectionDown);
  
   oCup.Visible = false;
   
   if (draino_in_cup) {
     receptionist_dead = 1;
     Wait(100);
     cSusan.Clickable = false;
     cSusan.Say("&11 Awwwwwk... Ugh...");
     cSusan.Say("&12 Cornnuts.");
     cSusan.x = cSusan.x+ 25;
     cSusan.ChangeView(23);
     cSusan.SetIdleView(23, 10);
     cSusan.Animate(0, 10, eOnce, eNoBlock, eForwards); 
     updateList(6);
     showList();
     cTeen.ChangeRoom(300);
   }
   
}

function room_Load()
{
  if (!receptionist_dead) {
  cSusan.SetIdleView(14, 8);
  }
    oCup.Visible = false; 

}

function room_AfterFadeIn()
{
    if (talked_to_teen == 2 || talked_to_teen == 5) {

    carAlarm = aCaralarm.Play(eAudioPriorityNormal, eRepeat); 
    Wait(15);
    cSusan.Say("&13 MY CAR!");
    cSusan.Say("&14 I'll be right back.");
    
    oCup.Visible = true;
   
   cSusan.Walk(295, 180, eBlock, eWalkableAreas);
   cSusan.Walk(264, 216, eBlock, eWalkableAreas);
   cSusan.Walk(10, 230, eBlock, eWalkableAreas);
   
   cSusan.ChangeRoom(4, 190, 209, eDirectionDown);
  }  
  
}

function room_RepExec()
{
  if (carAlarm != null) {
   
   if (carAlarm.IsPlaying) {
     alarmTimer++;
     booth = 1;
   }
    
  }
  
  if (alarmTimer > 500 && comm_done <=5 ) {  //#TODO
    
    booth = 0;
    alarmTimer = 0;
    aBoop.Play(eAudioPriorityNormal, eOnce);
    carAlarm.Stop();
    susanReturns();
  }
  
}


function hPoster_Interact()
{
  cDebra.Say("&197 Posters are for reading, not touching.");
}

function hPoster_Look()
{
  // Check if Mercury Motors is already in GPS
  for (int i = 0; i < lGPS.ItemCount; i++){
  
    if (lGPS.Items[i] == "Mercury Motors") {
      hasMercury = 1;
    }
     
  }

  cDebra.Say("&198 It's the station sponsor.. Mercury Motors.");
  cDebra.Say("&199 They sell used cars.");
  
  if (!hasMercury) {
      cDebra.Say("&200 I'll program their location in my GPS.");
      lGPS.AddItem("Mercury Motors");
  }
}

function hBooth_Look()
{
  cDebra.Say("&201 The door into the DJ broadcasting booth.");
  cDebra.Say("&202 They never let me in there. Jerks.");
}

function hBooth_Interact()
{
  //Check if she can go in now
  if (booth == 0) {  
    cSusan.Say("&15 Hey Debra! You can't go in there.");  
  }
  else if (booth == 1 && comm_done == 2)
  {
  cDebra.ChangeRoom(6, 273, 183);
  }
  else cDebra.Say("&203 I don't need to go in there.");
  
}

function hWater_Look()
{
  cDebra.Say("&204 Free water.");
}

function hWater_Interact()
{
  cDebra.Say("&205 I don't need any water.");
}

function hExit_Interact()
{
   if (carAlarm != null && carAlarm.IsPlaying && comm_done <=5) {
      cDebra.Say("&206 I better not leave while Susan is out there. She may suspect something.");
   }
  else
  cDebra.ChangeRoom(4, 120, 205, eDirectionDown);
}

function hPlaque_Look()
{
  cDebra.Say("&207 Plaque reads, '2016 Winner! Best local station'");
}


function oPaper_Look()
{
  cDebra.Say("&208 Susan has a stack of printer paper.");
}

function oPaper_Interact()
{
  cDebra.Say("&209 I don't think Susan would like me just taking her paper.");
}

function oCup_Look()
{
  cDebra.Say("&210 The paper cup Susan was drinking from.");
}

function oCup_Interact()
{
  if (comm_done == 5 && !receptionist_dead) {
    cDebra.Say("&211 She won't see me pour something in, if I'm quick.");  
  }
  else {
    cDebra.Say("&212 I don't need it.");
  }
}

function oCup_UseInv()
{
   if (cDebra.ActiveInventory == iDraino && !receptionist_dead && comm_done == 5 && !draino_in_cup) {
     cDebra.Walk(211, 191, eBlock, eWalkableAreas);
     cDebra.Say("&213 I'll pour some Draino in.");
     draino_in_cup = 1;
     cDebra.LoseInventory(iDraino);  
   }

}

function oPaper_UseInv()
{
  if (cDebra.ActiveInventory == iRing && receptionist_dead && comm_done == 8 && called_police) {
    
    cDebra.Say("&146 I'll place Skip's wedding ring on top.");
    cDebra.LoseInventory(iRing);
    
    updateList(9);
    showList();
  }
}
