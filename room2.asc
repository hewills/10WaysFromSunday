// room script file

//load before
function room_Load()
{

  if (comm_done >= 3 && comm_done <= 5 && !organist_dead) {
    
    //Move the decoration as the organist plays
    oOrgan.SetView(22);
    oOrgan.Animate(0, 6, eRepeat, eNoBlock, eForwards);
    
    //Organist plays
    oOrganist.Visible = true;
    oOrganist.SetView(21);
    oOrganist.Animate(0, 7, eRepeat, eNoBlock, eForwards);    
  }
  else if (comm_done > 5 && !organist_dead) {
      oOrgan.Visible = false;
      oOrganist.Visible = false;
  }
  else if (comm_done > 5 && organist_dead) {
      oOrgan.Visible = false;
      oOrganist.Visible = true;
  }
  else oOrganist.Visible = false;
  
}

//load after
function room_AfterFadeIn()
{
  if (comm_done >= 3) {
   if (!organist_dead && !receptionist_dead) {
     
    if (dminor != null) {
          
         if (dminor.IsPlaying) {
            dminor.Volume = 50;
         }
         else {
          dminor = a10Dminor.Play(); 
          dminor.Volume =50;
         }
    }
    else {
      dminor = a10Dminor.Play();
      dminor.Volume = 50;
       } 
   }
  }
}

function oOrgan_Look()
{ 
  if (comm_done >= 3) {
     cDebra.Say("&125 The lower notes cause it to rattle.");
     cDebra.Say("&126 Good thing he's not playing a more bass-heavy tune.");
     look_at_decoration = 1;
  }
  else {
  cDebra.Say("&127 Fancy organ has fancy decoration pieces.");
  }
}

function oWood_Look()
{
  cDebra.Say("&128 Extra wood, leftover from building new pews.");
}

function oNails_Look()
{
  cDebra.Say("&129 Box of nails.");
}

function oBucket_Look()
{
  cDebra.Say("&130 Empty bucket.");
}

function room_LeaveBottom()
{
  cDebra.ChangeRoom(13, 160, 100, eDirectionDown);
}

function hPodium_Look()
{
  cDebra.Say("&131 The pastor's podium.");
}

function hPodium_Interact()
{
  cDebra.Say("&132 I have no declarations to make.");
}

function hGlass_Look()
{
  cDebra.Say("&133 I think it's supposed to be abstract angels.");
}

function hOrgan_Look()
{
  cDebra.Say("&134 The church's beautiful pipe organ. It sounds amazing.");
}

function hOrgan_Interact()
{
  if (dminor != null && dminor.IsPlaying) {
    cDebra.Say("&135 The organ is taken right now."); 
  }
  else {
  cDebra.Walk(172, 200, eBlock, eWalkableAreas);
  cDebra.Say("&136 I only know one little piece of music...");
  aDebraorgan.Play(eAudioPriorityNormal, eOnce);
  Wait(300);  //Need a loop that waits for music to finish
  cDebra.Say("&137 Spooky.");
  }
}

function oWood_Interact()
{
  if (!cDebra.HasInventory(iWood)) {
    
    cDebra.Walk(237, 197, eBlock, eWalkableAreas);
    cDebra.Say("&138 I might need this wood.");
    cDebra.AddInventory(iWood);
    oWood.Visible = false;
  }
}

function oBucket_Interact()
{
  cDebra.Say("&139 I don't think I need it.");
}

function oNails_Interact()
{
    if (!cDebra.HasInventory(iNails)) {
  
    cDebra.Walk(237, 197, eBlock, eWalkableAreas);
    cDebra.Say("&140 I might need nails.");
    cDebra.AddInventory(iNails);
    oNails.Visible = false;
  } 
}

function oOrganist_Look()
{
  if ( !organist_dead ) {
    cDebra.Say("&141 It's the church organist. He seems pretty engrossed.");
  }
  else if (organist_dead) {
    cDebra.Say("&142 The wind was knocked out of him. And then some."); 
  }
  
}

function oOrganist_UseInv()
{
  if (cDebra.ActiveInventory == iMusic) {
    
    cDebra.Walk(204, 200, eBlock, eWalkableAreas);
    cDebra.Say("&143 Sorry to bother you, but why don't you try this piece?");
    cDebra.FaceDirection(eDirectionUp);
    cDebra.SayAt(100, 100, 150, "&144 Okay.");
    cDebra.LoseInventory(iMusic);
    cDebra.FaceDirection(eDirectionDown);
    
    aTocatta_organ.Play(eAudioPriorityNormal);
    
    Wait(1000);
    
    int y = oOrgan.Y;
    
    while ( y < 142 ) {
      Wait(20);
       oOrgan.Y = y;
       y++;
    }
    
    Wait(50);
    
    oOrgan.Visible = false;
    
    organist_dead = 1;
    dminor.Stop();
    
    oOrganist.StopAnimating();
    
    oOrganist.X = 159;
    oOrganist.Y = 209;
    oOrganist.Graphic = 184;
    
    Wait(50);
    cDebra.Say("&145 Damn.");
    
    updateList(6);
    showList();
    
  }

}

function hOrgan_UseInv()
{
  if (cDebra.ActiveInventory == iRing && organist_dead && comm_done == 8 && called_police) {
    
    cDebra.Say("&146 I'll place Skip's wedding ring on top.");
    cDebra.LoseInventory(iRing);
    
    updateList(9);
    showList();
  }



}
