// room script file

function room_Load()
{
  oCat.SetView(9);  
  oCat.Animate(0, 15, eRepeat, eNoBlock, eForwards);
  
  if (oFrame.Visible == true && oFrame.Graphic == 151) {
    //Hang it on the wall
   oFrame.X = 190;
   oFrame.Y = 88;
  }
  
  if (comm_done == 6 && cSkip.Room == 11) { //Skip is no longer in bar
    oSkipsleeping.Visible = true;
    oRing.Visible = true;  
  }
  
  if (comm_done > 6) {
    oSkipsleeping.Visible = false;
    oRing.Visible = false;
  }
  
  if (comm_done == 9) {
    cBob.ChangeRoom(3, 143, 129);    
  }
  
}

function room_AfterFadeIn()
{
   if (comm_done == 6 && cSkip.Room == 11) { //Skip is no longer in bar

    //Play snoring sound effect
    Wait(50);
    cDebra.Say("&147 I hope he doesn't expect breakfast.");
    updateList(7);
    showList();
  }
 
  if (satan_arrives == 1 && comm_done == 5) {
    
    oPoof_Debra.Visible = true;
    oPoof_Debra.SetView(20);
    oPoof_Debra.Animate(0, 20, eOnce, eBlock, eForwards);
    oPoof_Debra.Visible = false;
  
    cDebra.Say("&148 What's going on?");
    
    oPoof_Satan.Visible = true;
    oPoof_Satan.SetView(20, 0);
    Wait(40);
    cSatan.SayAt(200, 100, 100, "&1 SURPRISE!");
    Wait(40);
    cDebra.Say("&149 Finally! It's God! And just in time!");
    Wait(30);
    oPoof_Satan.Animate(0, 20, eOnce, eBlock, eForwards);
    cSatan.ChangeRoom(3, 244, 198, eDirectionDown);
    oPoof_Satan.Visible=false;
    
    Wait(50);
    cDebra.Say("&150 Oh..no.");
    Wait(50);
    cSatan.Say("&2 Okay, Okay, you got my attention.");
    cDebra.Say("&151 Yeeaaah.");
    Wait(50);
    cSatan.Say("&3 Wait...");
    Wait(70);
    cSatan.Say("&4 Were you trying to God's attention?");
    cSatan.Say("&5 HAHAHAHAHA!!");
    cSatan.Say("&6 That's hilarious!");
    cSatan.Say("&7 What were you thinking exactly?");
    
    cDebra.Say("&152 I was thinking I need to save my love Timothy.");
    cDebra.Say("&153 He's lost.");
    cDebra.Say("&154 Do you know where he is?");
    
    cSatan.Say("&8 I don't know where he is. I don't keep tabs on every soul.");
    
    cDebra.Say("&155 God was supposed to come, so I could ask him for help.");
    
    cSatan.Say("&9 Shut up for a second.");
    cSatan.Say("&10 I was going to turn you into a worm, but I kinda like where this is going.");
    cSatan.Say("&11 I'll tell you what? You finish up the 10, and I'll take you to God personally.");
    
    cDebra.Say("&156 Promise?");
    cSatan.Say("&12 Sure.");
    
    cDebra.Say("&157 Okay. I don't know what else to do.");
    cSatan.Say("&13 You're welcome.");
    cSatan.Say("&14 HEE HAHA HEEHee HAHAHA!");
    
    oPoof_Satan.Visible = true;
    oPoof_Satan.Animate(0, 20, eOnce, eBlock, eForwards);
    oPoof_Satan.Visible=false;
    cSatan.ChangeRoom(12, 244, 198, eDirectionDown);
    
    satan_arrives = 2;
      
  }
  
}

function Cat_Look()
{
  cDebra.Say("&158 My pretty kitty Samantha.");
}

function Cat_Interact()
{
  cDebra.Say("&159 She doesn't like to be picked up, unless it's her idea.");
}

function Cat_UseInv()
{
  cDebra.Say("&160 Samantha wouldn't appreciate that.");
}

function hDoor_Look()
{
  cDebra.Say("&103 It's the exit.");
}

function hDoor_Interact()
{
  if (comm_done == 7) {    
    cDebra.Say("&161 I have a chance to break the 8th.");
    cDebra.Say("&162 I should take advantage of that, before leaving.");    
  }
  else
  showGPS();  
  
}

function hTable_Look()
{
  cDebra.Say("&163 It's my art table with easel. I use it for painting and drawing.");
}

function hTable_Interact()
{
  if (!cDebra.HasInventory(iPhone)) {
    
    cDebra.Say("&164 My cellphone is in the drawer. I'll grab it.");
    cDebra.AddInventory(iPhone);
  }
  else {
    cDebra.Say("&165 My artist table is empty.");
  }
}

function hBed_Look()
{
  cDebra.Say("&166 My futon doubles as a bed.");
  
  if (oSkipsleeping.Visible == true) {
    
    cDebra.Say("&167 Skip is still asleep. I hope he leaves soon.");
    cDebra.Say("&168 He took his ring off last night.");
  }
}

function hBed_Interact()
{
  cDebra.Say("&169 I don't need to sleep.");
}

function hArt_Look()
{
  cDebra.Walk(250, 176, eBlock, eWalkableAreas);
  cDebra.Say("&170 I painted this one weekend. I used my easel on the artists table.");
}


function hClock_Look()
{
  cDebra.Say("&171 My clock is right two times a day.");
}

function hWindow_Look()
{
  cDebra.Say("&172 I can see my neighbor's fence out my window.");
}

function hWindow_Interact()
{
  cDebra.Say("&173 I rather keep my window closed.");
}

function hBookcase_Look()
{
  cDebra.Say("&174 I keep stuff rather than books on my bookcase.");
}

function hBookcase_Interact()
{
  cDebra.Walk(250, 176, eBlock, eWalkableAreas);
  
  if (!cDebra.HasInventory(iMarker) && comm_done < 2) {
    
    cDebra.Say("&175 I have my craft supplies in here. I'll grab them just in case.");
    cDebra.AddInventory(iMarker);
    cDebra.AddInventory(iScissors);  
  }
  else {
    cDebra.Say("&176 There isn't anything else I need here.");
  }
}

function oFrame_Look()
{
  cDebra.Say("&177 My try at a graven image.");
}

function oFrame_Interact()
{
  cDebra.Walk(153, 201, eBlock, eWalkableAreas);
  
  if (oFrame.Graphic != 151) {  //Does not have EGO picture 
    cDebra.Say("&178 I can't draw with my finger.");
  }
  else cDebra.Say("&179 It's sturdy.");
  
}

function oFrame_UseInv()
{
   cDebra.Walk(153, 201, eBlock, eWalkableAreas);
   
   if (cDebra.ActiveInventory == iMarker) {
     
     cDebra.Say("&180 Well... here goes nothing.");
     Wait(15);
     cDebra.LoseInventory(iMarker);
     oFrame.Graphic = 151;  //Frame with EGO God
     updateList(2);
     showList();
     
     //Now that the 2nd commandment is done. Teen shows up.
     cTeen.ChangeRoom(4, 69, 193, eDirectionDown);
     
   }  
}

//Put frame on table (if paper inside)
function hTable_UseInv()
{
   cDebra.Walk(153, 201, eBlock, eWalkableAreas);
  
  if (cDebra.ActiveInventory == iFrame && iFrame.Graphic == 150) {  //Frame with paper
    
    cDebra.LoseInventory(iFrame);
    oFrame.Visible = true;
    
  }
  else if (cDebra.ActiveInventory == iFrame && iFrame.Graphic != 150) {  //Frame with NO paper
    cDebra.Say("&181 Wait, my frame is still empty. I need something to draw on.");    
  }
  else cDebra.Say("&182 That's weird even for me.");

}



function oRing_Look()
{
  cDebra.Say("&183 Skip took his wedding ring off last night.");
  
}

function oRing_Interact()
{
  if (!cDebra.HasInventory(iRing)) {
     cDebra.Say("&184 Hmm.. might as well take care of the 8th Commandment.");
     cDebra.AddInventory(iRing);
      oRing.Visible = false;
      
     updateList(8);
     showList();
 }  
  
}
