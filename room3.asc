// room script file



function room_Load()
{
  oCat.SetView(9);  
  oCat.Animate(0, 15, eRepeat, eNoBlock, eForwards);
  
  if (oFrame.Visible == true && oFrame.Graphic == 151) {
    //Hang it on the wall
   oFrame.X = 190;
   oFrame.Y = 88;
  }
  
}

function room_AfterFadeIn()
{


  if (satan_arrives == 1 && comm_done == 5) {
    
    oPoof_Debra.Visible = true;
    oPoof_Debra.SetView(20);
    oPoof_Debra.Animate(0, 20, eOnce, eBlock, eForwards);
    oPoof_Debra.Visible = false;
  
    cDebra.Say("What's going on?");
    
    oPoof_Satan.Visible = true;
    oPoof_Satan.SetView(20, 0);
    Wait(40);
    cSatan.SayAt(200, 100, 100, "SURPRISE!");
    Wait(40);
    cDebra.Say("Finally! It's God! And just in time!");
    Wait(30);
    oPoof_Satan.Animate(0, 20, eOnce, eBlock, eForwards);
    cSatan.ChangeRoom(3, 244, 198, eDirectionDown);
    oPoof_Satan.Visible=false;
    
    Wait(50);
    cDebra.Say("Oh..no.");
    Wait(50);
    cSatan.Say("Okay, Okay, you got my attention.");
    cDebra.Say("Yeeaaah.");
    Wait(50);
    cSatan.Say("Wait...");
    Wait(70);
    cSatan.Say("Were you trying to God's attention?");
    cSatan.Say("HAHAHAHAHA!!");
    cSatan.Say("That's hilarious!");
    cSatan.Say("What were you thinking exactly?");
    
    cDebra.Say("I was thinking I need to save my love Timothy.");
    cDebra.Say("He's lost.");
    cDebra.Say("Do you know where he is?");
    
    cSatan.Say("I don't know where he is. I don't keep tabs on every soul.");
    
    cDebra.Say("God was supposed to come, so I could ask him for help.");
    
    cSatan.Say("Shut up for a second.");
    cSatan.Say("I was going to turn you into a worm, but I kinda like where this is going.");
    cSatan.Say("I'll tell you what? You finish up the 10, and I'll take you to God personally.");
    
    cDebra.Say("Promise?");
    cSatan.Say("Sure.");
    
    cDebra.Say("Okay. I don't know what else to do.");
    cSatan.Say("You're welcome.");
    cSatan.Say("HEE HAHA HEEHee HAHAHA!");
    
    oPoof_Satan.Visible = true;
    oPoof_Satan.Animate(0, 20, eOnce, eBlock, eForwards);
    oPoof_Satan.Visible=false;
    cSatan.ChangeRoom(12, 244, 198, eDirectionDown);
    
  }  
  
  satan_arrives = 2;
  
}

function Cat_Look()
{
  cDebra.Say("My pretty kitty Samantha.");
}

function Cat_Interact()
{
  cDebra.Say("She doesn't like to be picked up, unless it's her idea.");
}

function Cat_UseInv()
{
  cDebra.Say("Samantha wouldn't appreciate that.");
}

function hDoor_Look()
{
  cDebra.Say("It's the exit.");
}

function hDoor_Interact()
{
  showGPS();
}

function hTable_Look()
{
  cDebra.Say("It's my art table with easel. I use it for painting and drawing.");
}

function hTable_Interact()
{
  if (!cDebra.HasInventory(iPhone)) {
    
    cDebra.Say("My cellphone is in the drawer. I'll grab it.");
    cDebra.AddInventory(iPhone);
  }
  else {
    cDebra.Say("My artist table is empty.");
  }
}

function hBed_Look()
{
  cDebra.Say("My futon doubles as a bed.");
}

function hBed_Interact()
{
  cDebra.Say("I don't need to sleep.");
}

function hArt_Look()
{
  cDebra.Walk(250, 176, eBlock, eWalkableAreas);
  cDebra.Say("I painted this one weekend. I used my easel on the artists table.");
}


function hClock_Look()
{
  cDebra.Say("My clock is right two times a day.");
}

function hWindow_Look()
{
  cDebra.Say("I can see my neighbor's fence out my window.");
}

function hWindow_Interact()
{
  cDebra.Say("I rather keep my window closed.");
}

function hBookcase_Look()
{
  cDebra.Say("I keep stuff rather than books on my bookcase.");
}

function hBookcase_Interact()
{
  cDebra.Walk(250, 176, eBlock, eWalkableAreas);
  
  if (!cDebra.HasInventory(iMarker)) {
    
    cDebra.Say("I have my craft supplies in here. I'll grab them just in case.");
    cDebra.AddInventory(iMarker);
    cDebra.AddInventory(iScissors);  
  }
  else {
    cDebra.Say("There isn't anything else I need here.");
  }
}

function oFrame_Look()
{
  cDebra.Say("My try at a graven image.");
}

function oFrame_Interact()
{
  cDebra.Walk(153, 201, eBlock, eWalkableAreas);
  
  if (oFrame.Graphic != 151) {  //Does not have EGO picture 
    cDebra.Say("I can't draw with my finger.");
  }
  else cDebra.Say("It's sturdy.");
  
}

function oFrame_UseInv()
{
   cDebra.Walk(153, 201, eBlock, eWalkableAreas);
   
   if (cDebra.ActiveInventory == iMarker) {
     
     cDebra.Say("Well... here goes nothing.");
     Wait(15);
     cDebra.LoseInventory(iMarker);
     oFrame.Graphic = 151;  //Frame with EGO God
     updateList(2);
     showList();
     
     //Now that the 2nd commandment is done. Teen shows up.
     cTeen.ChangeRoom(4, 69, 193, eDirectionDown);
     
   }  
}

//Put frame on table (if paper inside)
function hTable_UseInv()
{
   cDebra.Walk(153, 201, eBlock, eWalkableAreas);
  
  if (cDebra.ActiveInventory == iFrame && iFrame.Graphic == 150) {  //Frame with paper
    
    cDebra.LoseInventory(iFrame);
    oFrame.Visible = true;
    
  }
  else if (cDebra.ActiveInventory == iFrame && iFrame.Graphic != 150) {  //Frame with NO paper
    cDebra.Say("Wait, my frame is still empty. I need something to draw on.");    
  }
  else cDebra.Say("That's weird even for me.");

}


